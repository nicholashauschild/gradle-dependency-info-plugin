buildscript {
    ext.kotlin_version = '1.1.4'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

plugins {
    // kotlin lang
    id 'org.jetbrains.kotlin.jvm' version '1.1.4'

    // publishing artifactory/bintray
    id "com.jfrog.bintray" version "1.7.3"
    id "com.jfrog.artifactory" version "4.4.18"

    // plugin development/publishing
    id "com.gradle.plugin-publish" version "0.9.7"
    id "java-gradle-plugin"
}

apply plugin: 'maven-publish'
apply plugin: 'org.junit.platform.gradle.plugin'


/**
 * Property definitions
 */

group = 'com.github.nicholashauschild'
version = file('version.txt').text.trim()

def bintrayUsername = hasProperty('bintray_username') ? project.getProperty('bintray_username') : ''
def bintrayApiKey = hasProperty('bintray_apikey') ? project.getProperty('bintray_apikey') : ''
def isSnapshot = version.endsWith('SNAPSHOT')
def isRelease = !isSnapshot


/**
 * Dependencies
 */

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    //testCompile gradleTestKit()
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M4'
    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M4'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile 'org.jetbrains.spek:spek-api:1.1.2'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M4'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'
}


/**
 * Kotlin configuration
 */

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


/**
 * Testing configuration
 */

junitPlatform {
    filters {
        engines {
            include 'spek'
            include 'junit-jupiter'
        }
    }
}


/**
 * Wrapper configuration
 */

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}


/**
 * Artifact configuration
 */

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}


/**
 * Publishing configuration
 */

def pomConfig = {
    name 'gradle-dependency-info-plugin'
    url 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'nicholashauschild'
            name 'Nicholas Hauschild'
            email 'nicholas.hauschild@gmail.com'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin to package dependency details with a jar')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUsername
            password = bintrayApiKey
        }
        defaults {
            publications 'plugin'
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey

    publications = ['plugin']
    publish = true

    pkg {
        repo = 'maven'
        name = 'gradle-dependency-info-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin.git'

        desc = 'Gradle plugin to include dependency information in generated jar'
        labels = ['gradle', 'spring-boot', 'plugin', 'java']
        websiteUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
        issueTrackerUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin/issues'
    }
}

pluginBundle {
    website = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'
    vcsUrl = 'https://github.com/nicholashauschild/gradle-dependency-info-plugin'

    plugins {
        plugin {
            id = 'com.github.nicholashauschild.dependency-info'
            displayName = 'Dependency Info Plugin'

            description = 'Gradle plugin to include dependency information in generated jar'
            tags = ['java', 'spring-boot']
        }
    }
}

/**
 * This is a workaround until the com.gradle.plugin-publish is updated to allow
 * specifying key/secret via the pluginBundle extension.
 *
 * Workaround from: https://github.com/gradle/gradle/issues/1246
 */

task setupPluginUpload {
    doLast {
        def key=System.env.gradlePublishKey
        def secret = System.env.gradlePublishSecret

        if( !key || !secret)
        {
            throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
        }

        System.properties.setProperty("gradle.publish.key", key)
        System.properties.setProperty("gradle.publish.secret", secret)
    }
}

tasks.publishPlugins.dependsOn tasks.setupPluginUpload